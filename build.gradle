apply plugin: 'java'
apply plugin: 'eclipse'

/*
 * HELLO! This build script works with Gradle 1.0-milestone-8 and 1.0-milestone-9. It does not work with 1.0-milestone-3. 
 */

sourceCompatibility = 1.6
targetCompatibility = 1.6

group = 'com.timgroup'
def majorVersion = 1
def minorVersion = 0
def isReleaseVersion = System.getenv('BUILD_NUMBER') != null
def patchVersion = isReleaseVersion ? System.getenv('BUILD_NUMBER') : new Date().format('yyyyMMdd-HHmmss', TimeZone.getTimeZone("UTC"))
def patchSeparator = isReleaseVersion ? '.' : '-'
version = "${majorVersion}.${minorVersion}${patchSeparator}${patchVersion}"

repositories {
    mavenLocal()
    mavenRepo url: "${repoUrl}/groups/public"
}

def slf4jVersion = '1.6.4'

// some of these dependencies are optional, in that they are needed to build, but might not be needed to run; Gradle does currently let us say so
dependencies {
    compile group: 'org.slf4j', name: 'slf4j-api', version: slf4jVersion
    compile group: 'com.yammer.metrics', name: 'metrics-core', version: '2.0.2' // optional
    compile group: 'javax.servlet', name: 'servlet-api', version: '2.5' // optional
    
    testCompile group: 'junit', name: 'junit', version: '4.10'
    testCompile group: 'xerces', name: 'xercesImpl', version: '2.9.1'
    testCompile group: 'org.mockito', name: 'mockito-core', version: '1.8.5'
    
    testRuntime group: 'org.slf4j', name: 'slf4j-simple', version: slf4jVersion
}

task sourcesJar(type: Jar, dependsOn:classes) {
     classifier = 'sources'
     from sourceSets.main.allSource
}

eclipse {
    classpath {
        defaultOutputDir = project.sourceSets.main.output.classesDir
    }
}

task hgignore << {
	// gitignore and hgignore syntax are similar enough that this works (for now)
	file('.hgignore').withWriter { out ->
		out.println 'syntax: glob'
		out.println '.hgignore'
		file('.gitignore').eachLine { line ->
			out.println line
		}
	}
}

